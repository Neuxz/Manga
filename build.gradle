
group 'de.herrlock.manga'
version '1.3.0-BETA-SNAPSHOT'

subprojects {
	apply plugin: 'java'
	apply plugin: 'findbugs'
	apply plugin: 'pmd'
	apply plugin: 'maven-publish'
	apply plugin: 'eclipse'
	
	sourceCompatibility = "1.7"
	
	jar.dependsOn check
	
	dependencies {
		testCompile 'junit:junit:4+'
	}
	
	findbugs {
		effort = 'max'
		reportLevel = 'low'
	}
	tasks.withType(FindBugs) {
	    reports {
	        xml.enabled = false
	        html.enabled = true
	    }
	}
	
	eclipse.project {
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
		buildCommand 'edu.umd.cs.findbugs.plugin.eclipse.findbugsBuilder'
	}
	
	repositories {
		mavenCentral()
	}
}

task eclipse << {
    file('.project').text = '<?xml version="1.0" encoding="UTF-8"?><projectDescription><name>Manga</name></projectDescription>'
}

task clean (type: Delete) {
	delete 'build/'
}

def temp = 'build/temp/'

task createLibs (type: Copy) {
	into temp + 'lib'
    subprojects.each { s -> 
		dependsOn s.jar
		from s.jar
		from s.configurations.runtime
	}
}

task copyOther (type: Copy) {
	from 'LICENSE'
	
	into temp
}

task copyDist (type: Copy) {
	from 'src/dist'
	into temp
}

task dist (type: Zip, dependsOn: ['createLibs', 'copyOther', 'copyDist']) {
    destinationDir = file('build')
    baseName = project.name
    version = project.version
    
    into baseName + '-' + version
    from temp
}
