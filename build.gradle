
allprojects {
	apply plugin: 'eclipse'
	
	group 'de.herrlock'
	version '1.4.0-BETA-SNAPSHOT'
}

apply from: 'subprojects.gradle'

task clean (type: Delete, group: 'build', description: 'Deletes the build directory. (R)') {
	delete 'build/'
}

def temp = 'build/temp/'

task createLibs (type: Copy, description: 'Create jar from subprojects and copies them into the lib-folder') {
	into temp + 'lib'
    subprojects.each { s -> 
		dependsOn s.jar
		dependsOn s.check
		from s.jar
		from s.configurations.runtime
	}
}

task copyOther (type: Copy, description: 'Copy other resources to the temp-folder') {
	from 'LICENSE'
	into temp
}

task copyDist (type: Copy, description: 'Copy content of \"src/dist\" to the temp-folder') {
	from 'src/dist'
	into temp
}

task compileRun (description: 'Compile run.cs and copy the exe into the build-folder') {
    def folder = 'src/main/cs'
    def filename = 'Run.exe'
    def exefile = new File(folder, filename)
    def target = mkdir('build/temp/')
    doLast {
        exec {
            workingDir folder
            commandLine 'cmd', '/c', 'csc', '/t:winexe', "/out:${filename}", '/nologo', 'run.cs'
        }
        copy {
            from exefile
            into target
        }
        delete exefile
    }
}

task dist (type: Zip, group: 'build', description: 'Creates a distribution in a zip-file', dependsOn: [createLibs, copyOther, copyDist, compileRun]) {
    destinationDir = file('build')
    baseName = project.name
    version = project.version
    
    into baseName + '-' + version
    from temp
}

